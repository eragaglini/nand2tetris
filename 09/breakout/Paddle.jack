
/** Implements a graphical Paddle. */
class Paddle {

   field int x, y; // screen location of the Paddle's top-left corner

   /** Constructs a new Paddle with a given location. */
   constructor Paddle new() {
      let x = 231;
      let y = 230;
      do draw();
      return this;
   }

   method int getX() {
    return x;
   }

   method int getY() {
    return y;
   }

   /** Disposes this Paddle. */
   method void dispose() {
      do Memory.deAlloc(this);
      return;
   }

   /** Draws the Paddle on the screen. */
   method void draw() {
		do Screen.setColor(true);
		do Screen.drawRectangle(x, y, x + 50, y + 10);
      return;
   }

   /** Erases the Paddle from the screen. */
   method void erase() {
		do Screen.setColor(false);
		do Screen.drawRectangle(x, y, x + 50, y + 10);
      return;
   }

   /** Moves the Paddle left by 2 pixels. */
   method void moveLeft() {
        if (x > 1) {
            do erase();
            let x = x - 2;
            do draw();
        }
        return;
   }

   /** Moves the Paddle right by 2 pixels. */
    method void moveRight() {
        //if (x < 510) {
         if (x < 460) {
            do erase();
            let x = x + 2;
            do draw();
        }
        return;
    }
}
